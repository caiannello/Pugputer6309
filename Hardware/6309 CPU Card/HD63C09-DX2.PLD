Name            HD63C09-DX2 ;
Partno          ATF16V8B ;
Revision        03 ;
Date            7/22/2022 ;
Designer        Craig Iannello ;
Company         Pugbutt Industries LLC ;
Assembly        CPU Card ;
Location        IC2 ;
Device          g16v8a ;

/********************************************************************************/
/* This device decodes the memory address for the HD6309 computer v0.1          */
/*                                                                              */
/* RAM: 0000 - 7FFF                                                             */
/* UART:F000 - F1FF                                                             */
/* AUX0:F200 - F3FF                                                             */
/* AUX1:F400 - F5FF                                                             */
/* VDP: F600 - F7FF  (decoded by video card)                                    */
/* ETC: F800 - F9FF  (decoded by card)                                          */
/* ETC: FA00 - FBFF  (decoded by card)                                          */
/* ETC: FC00 - FDFF  (decoded by card)                                          */
/* ROM: 8000 - FFFF EXCEPT WHEN ZEROES IN ANY OF A11..A9                        */
/*                                                                              */
/* Compile using WINCUPL (http://www.atmel.com/tools/wincupl.aspx               */
/*                                                                              */
/********************************************************************************/
/* input signals - we don't use special clock PIN 1 */

pin 2      = E ;            /* E clock from HD6309              */
pin 3      = RW ;           /* R/~W pin from HD6309             */
pin 4      = a9;
pin 5      = a10;
pin 6      = a11;
pin 7      = a12;
pin 8      = a13;
pin 9      = a14;
pin 11     = a15;
/* outputs (available: 12..19) 
   Note: negative outputs don't work
   as reported here: 
   http://ecee.colorado.edu/~mcclurel/WinCUPL_Intro_handouts2.pdf
   so we define them as active high
   but code them as active low!
*/

pin 19     = read_n;
pin 18     = write_n;
pin 17     = ram_cs_n;
pin 16     = rom_cs_n;
pin 15     = uart_cs_n;
pin 14     = led;
pin 13     = aud_n;
pin 12     = vdp_n;   

/** declarations **/

/** equations **/

read_n    = !E # !RW;
write_n   = !E # RW;
ram_cs_n  = !E # a15;
uart_cs_n = !E # !a15 # !a14 # !a13 # !a12 # a11 # a10 # a9;
aud_n     = !E # !a15 # !a14 # !a13 # !a12 # a11 # a10 # !a9;
vdp_n     = !E # !a15 # !a14 # !a13 # !a12 # a11 # !a10 # a9;
led       = !E # !a15 # !a14 # !a13 # !a12 # a11;
rom_cs_n  = !E # !a15 # !RW # (!a14 & !a13 & !a12 & a11);
