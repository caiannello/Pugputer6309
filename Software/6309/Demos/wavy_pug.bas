10 F1=0:F2=0:REM ************* MULTICOLOR WAVY PUG!!!! ***************
20 POKE &HA91,0:EXEC &HB003:REM HIDE CURSOR AND PAUSE SCREEN REFRESH
30 POKE &HF401,0:POKE&HF401,&H90:POKE &HF402,&H00:POKE&HF402,&H00:REM BLACK BG
40 FOR R = 1 TO 3:S$ = "":REM MAKE PUG CHAR ARRAY FOR SPEED
50 READ V:IF V<0 THEN Q$(R)=S$:S$="":NEXT R ELSE S$=S$+CHR$(V):GOTO 50
60 EXEC &HB000:REM RESUME SCREEN REFRESH (VBLANK INTERRUPT)
70 X=35*SIN(T)+40:T=T+0.30 : REM MAIN LOOP BEGINS
80 F1=F1+1:IF F1>255 THEN F1=0:F2=F2+1:IF F2>255 THEN F2=0:REM NEW FG COLOR
90 POKE &HF401,1:POKE&HF401,&H90:POKE &HF402,F1:POKE&HF402,F2 : REM SET FG CLR
100 PRINTTAB(X-4);Q$(1):PRINTTAB(X-4);Q$(2):PRINTTAB(X-4);Q$(3):REM PRINT PUG
110 A$=INKEY$:IFA$=""THEN 70 : REM MAINLOOP UNTIL KEY PRESSED.
120 EXEC &HB003 : REM CLEANUP TIME. PAUSE SCREEN REFRESH,
130 POKE &HF401,0:POKE&HF401,&H90:REM RESTORE COLORS
140 POKE &HF402,&H12:POKE&HF402,&H01:POKE &HF402,&H57:POKE&HF402,&H05
150 POKE &HA91,3:EXEC &HB000:END:REM ENABLE CURSOR AND DISPLAY REFRESH
160 DATA 1,2,4,7,10,0,15,17,20,-1,32,3,5,23,11,14,16,18,21,-1
170 DATA 32,32,6,9,12,32,32,19,22,-1:REM <- PUG CHAR DATA ^
